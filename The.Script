//@version=5 WickDipper
indicator("BTC.D Dominance Regime (Daily)", overlay=false)

sym = input.symbol("CRYPTOCAP:BTC.D", "BTC.D Symbol")

fastLen = input.int(20, "Fast EMA", minval=1)
slowLen = input.int(50, "Slow EMA", minval=1)

normLen = input.int(60, "Normalization Length", minval=10)
slopeLen = input.int(5, "Slope Smoothing", minval=1)

smoothLen = input.int(5, "Oscillator Smoothing", minval=1)
signalLen = input.int(9, "Signal Smoothing", minval=1)

wDiff = input.float(1.2, "Weight: Trend Spread", step=0.1)
wSlope = input.float(0.8, "Weight: Trend Slope", step=0.1)
wPos  = input.float(0.5, "Weight: Level vs Mean", step=0.1)

tanhf(x) =>
    xc = math.max(math.min(x, 10.0), -10.0)
    e2 = math.exp(2.0 * xc)
    (e2 - 1.0) / (e2 + 1.0)

d = request.security(sym, "D", close, barmerge.gaps_on, barmerge.lookahead_off)

fast = ta.ema(d, fastLen)
slow = ta.ema(d, slowLen)

diff = fast - slow
diffZ = (diff - ta.sma(diff, normLen)) / math.max(ta.stdev(diff, normLen), 1e-10)

slope = ta.sma(ta.change(fast), slopeLen)
slopeZ = (slope - ta.sma(slope, normLen)) / math.max(ta.stdev(slope, normLen), 1e-10)

pos = d - ta.sma(d, normLen)
posZ = (pos - ta.sma(pos, normLen)) / math.max(ta.stdev(pos, normLen), 1e-10)

raw = diffZ * wDiff + slopeZ * wSlope + posZ * wPos

osc = tanhf(raw) * 100.0
oscSm = ta.ema(osc, smoothLen)
sig = ta.ema(oscSm, signalLen)

plot(oscSm, "BTC.D Regime", linewidth=2, color=oscSm >= 0 ? color.lime : color.red)
plot(sig, "Signal", linewidth=2, color=color.gray)

hline(0, "Zero", color=color.new(color.white, 70))
hline(50, "BTC Lead Strong", color=color.new(color.lime, 80))
hline(-50, "Alt Rotation Strong", color=color.new(color.red, 80))

plotshape(ta.crossover(oscSm, 0), title="Cross Up", style=shape.triangleup, location=location.bottom, size=size.tiny, color=color.lime, text="BTC")
plotshape(ta.crossunder(oscSm, 0), title="Cross Down", style=shape.triangledown, location=location.top, size=size.tiny, color=color.red, text="ALTS")
